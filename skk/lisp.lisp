(in-package :cl-user)
(defpackage :cl-skkserv/skk/lisp
  (:use :cl :cl-ppcre :esrap :alexandria)
  (:import-from :cl-skkserv/core/main dictionary convert complete)
  (:import-from :cl-skkserv/skk/util make-table)
  (:export skk-lisp-dictionary lispp))
(in-package :cl-skkserv/skk/lisp)

(defclass skk-lisp-dictionary (dictionary)
  ((skk-lisp-dictionary-pathname :initarg :pathname :reader skk-lisp-dictionary-pathname)
   (skk-lisp-dictionary-table :accessor skk-lisp-dictionary-table)))

(defun lispp (s) (scan "^\\(.*\\)$" s))

(defmethod initialize-instance :after ((d skk-lisp-dictionary) &rest initargs)
  (declare (ignore initargs))
  (setf (skk-lisp-dictionary-table d) (make-table (skk-lisp-dictionary-pathname d)))
  (maphash (lambda (key value)
             (setf (gethash key (skk-lisp-dictionary-table d))
                   (remove-if-not #'lispp value))
             (unless (gethash key (skk-lisp-dictionary-table d))
               (remhash key (skk-lisp-dictionary-table d))))
           (skk-lisp-dictionary-table d)))

(defun concat (&rest s) (format nil "窿螬ㄤ彐礤翳镤泔铞弪狃疱钿è箅氕扉箴溟泗轱钺蝙篝蜷铉┅戾舄è汜钿殇狒弩ㄧ弭栳箬箅氕扉箴溟泗轱钺蝙翎忪洎┅í疳汶徵濯ㄦ轭洵疳汶徵恒飙箅塍弪霪箅氙扉箴┅灬忮祗è镢翦舡麸汨狎磲翥桢溟玳趔ㄤ邈灬蝈ㄩ珙矧磲翥桢螬痱轭悱麸篝蜷铉ㄣ镤瀛汨狎疳蝮瀛轭翦珏溟玳趔候徜轼俯┅镢翦舡麸汨狎ㄣ犷溟溽翦蝈珏蝈痨徙瀛犰④苘馨ㄜ茕苘洎汜钿殇狒＇镢翦舡麸汨狎后轫痨瀛汜祆舂┅磲疸狎ㄣ镯痫箦＇弼犰＇蝈徜骝镯篝蜷铉＇镢翦舡麸汨狎汜钿殇狒弩┅┅ㄤ彐礤翳镤泔眇戾翦狃疱钿è箅氕扉箴溟泗轱钺蝙篝蜷铉┅祜镳烘矧脲衡彘铉呼桢鸿狍璀脲猴箅氕扉箴溟泗轱钺蝙翎忪洎瑚桢筱犷ㄦ矧磲铋⑥幄螬脲恒镬戾泗脲┅